plugins {
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def cfg = rootProject.ext
if (rootProject.ext.isDebug) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdk cfg.android.compileSdk

    defaultConfig {
        if (cfg.isDebug)
            applicationId cfg.applicationId.main

        minSdk cfg.android.minSdk
        targetSdk cfg.android.targetSdk
        versionCode cfg.android.versionCode
        versionName cfg.android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ['-Xjvm-default','enable']
    }

    sourceSets {
        main {
            if (cfg.isDebug){
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
}

kapt {
    arguments {
        //使用+=
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

dependencies {
    implementation project(':modulesPublic:common')

    cfg.libs.each{key,value -> implementation value}
    cfg.apts.each{key,value -> kapt value}
}